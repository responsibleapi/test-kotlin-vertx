plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.25'
    id "com.github.ben-manes.versions" version "0.51.0"
    id 'maven-publish'
}

group = 'responsible'
version = '1.0.4'

repositories {
    mavenCentral()
}

dependencies {
    api(
        "org.openapi4j:openapi-operation-validator:1.0.7",
    )

    implementation(platform("io.vertx:vertx-stack-depchain:4.5.10"))
    api(
        "io.vertx:vertx-lang-kotlin-coroutines",
        "io.vertx:vertx-web-client",
        'io.vertx:vertx-web-openapi',
    )

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach {
    kotlinOptions {
        allWarningsAsErrors = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

dependencyUpdates {
    checkConstraints = true

    rejectVersionIf {
        final version = it.candidate.version.toLowerCase()

        [
            "m", // milestone
            "alpha",
            "beta",
            "eap",
            "rc",
            "cr",
        ].any { version.contains(it) }
    }
}
